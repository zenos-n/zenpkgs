sw = Window.GetWidth();
sh = Window.GetHeight();

// calculate a scaling factor 
// based on a design reference resolution of 1200px width
s = sh / 1200;

fun setup_sprite(img_file) {
    img = Image(img_file);

    return Sprite(img.Scale(img.GetWidth() * s * 0.5, img.GetHeight() * s * 0.5));
}

// setup background
bg = setup_sprite("watermark_bg.png");
bg.SetX(-100 * s);
bg.SetY(604 * s);
bg.SetZ(-10);

// glow
glow = setup_sprite("glow.png");
glow.SetX((507 * s) - (glow.GetImage().GetWidth() / 2));
glow.SetY((1331 * s) - (glow.GetImage().GetHeight() / 2));
glow.SetZ(-5);

// top level elements
top_icon = setup_sprite("icon_top.png");
top_text = setup_sprite("host_text.png");

gap_top_h = 20 * s;

top_w = top_icon.GetImage().GetWidth() + gap_top_h + top_text.GetImage().GetWidth();

top_h = Math.Max(top_icon.GetImage().GetHeight(), top_text.GetImage().GetHeight());

lbl = setup_sprite("powered_by.png");
btm_icon = setup_sprite("icon_bottom.png");
os_name = setup_sprite("os_name.png");
os_ver = setup_sprite("os_version.png");

gap_btm = 8 * s;

btm_row_w = btm_icon.GetImage().GetWidth() + gap_btm + os_name.GetImage().GetWidth() + os_ver.GetImage().GetWidth();

btm_group_w = Math.Max(lbl.GetImage().GetWidth(), btm_row_w);

btm_group_h = lbl.GetImage().GetHeight() + gap_btm + Math.Max(btm_icon.GetImage().GetHeight(), os_name.GetImage().GetHeight());

// centering stuff
total_w = Math.Max(top_w, btm_group_w);
total_h = top_h + gap_btm + btm_group_h;

start_x = (sw - total_w) / 2;
start_y = (sh - total_h) / 2;

// positioning top elements
top_row_x = start_x + (total_w - top_w) / 2;

top_icon.SetX(top_row_x);
top_icon.SetY(start_y + (top_h - top_icon.GetImage().GetHeight())/2); // Vertically center in row

top_text.SetX(top_icon.GetX() + top_icon.GetImage().GetWidth() + gap_top_h);
top_text.SetY(start_y + (top_h - top_text.GetImage().GetHeight())/2); // Vertically center in row

// positioning bottom elements
btm_y = start_y + top_h + gap_btm;
btm_x = start_x + (total_w - btm_group_w) / 2;

lbl.SetX(btm_x + (btm_group_w - lbl.GetImage().GetWidth())/2);
lbl.SetY(btm_y);

row_x = btm_x + (btm_group_w - btm_row_w) / 2;
row_y = btm_y + lbl.GetImage().GetHeight() + gap_btm;

btm_icon.SetX(row_x);
btm_icon.SetY(row_y);

os_name.SetX(row_x + btm_icon.GetImage().GetWidth() + gap_btm);
os_name.SetY(row_y);

os_ver.SetX(os_name.GetX() + os_name.GetImage().GetWidth());
os_ver.SetY(row_y);

// animation loop
progress = 0;

fun refresh_callback () {
    progress++;

    glow.SetOpacity(0.6 + (Math.Sin(progress / 30) * 0.3));
}

Plymouth.SetRefreshFunction(refresh_callback);